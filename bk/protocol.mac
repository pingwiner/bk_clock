.extern sendCommand, delay, sendString, recvString, sendBlock, recvBlock, sendByte, recvByte


PORT = 177714; 		Extension port address


;Send command with data buffer
; r0 - command code
; r3 - buffer address
; r4 - buffer size
; Uses r0, r1, r2, r3, r4
sendCommand:
	call sendByte
	call sendBlock
	call recvByte
	rts pc

;Time delay between requests
; Uses r1
delay:
	mov #500, r1 
1:	
	dec r1
	bne 1
	rts pc

;Send string
; r3 - string address
; Uses r0, r1, r2, r3
sendString:
	movb (r3), r0
	call sendByte		
	bitb #0xff, (r3)	
	beq 1
	inc r3
	br sendString
1:	
	rts pc

;Receive string
; r3 - buffer address
; Uses r0, r1, r2, r3
recvString:
	call recvByte	
	movb r0, (r3)+	
	bitb #0xff, r0	
	bne recvString
	rts pc

;Send block
; r3 - buffer address, r4 - buffer size
; Uses r0, r1, r2, r3, r4
sendBlock:
	movb (r3)+, r0
	call sendByte	
	dec r4
	bne sendBlock
	rts pc

;Receive block
; r3 - buffer address, r4 - buffer size
; Uses r0, r1, r2, r3, r4
recvBlock:
	call recvByte	
	movb r0, (r3)+
	dec r4
	bne recvBlock
	rts pc

;Send byte
; r0 - byte to send
; Uses registers r0, r1, r2
sendByte:
	mov #PORT, r2
	mov r0, r1
	bic #0xfff0, r1
	bis #0x10, r1
	mov r1, (r2)

	mov r0, r1
	asr r1
	asr r1    
	asr r1
	asr r1
	bic #0xfff0, r1
	mov r1, (r2)
	rts pc

;Receive byte
;return
; r0 - received byte
; Uses registers r0, r1, r2
recvByte:
	mov #PORT, r2
1:
	bit #0x10, (r2)
	beq 1
	mov (r2), r0	
	bic #0xfff0, r0
2:	
	bit #0x10, (r2)
	bne 2
	mov (r2), r1
	asl r1
	asl r1    
	asl r1
	asl r1
	bic #0xff0f, r1
	bis r1, r0
	com r0
	bic #0xff00, r0
	rts pc

